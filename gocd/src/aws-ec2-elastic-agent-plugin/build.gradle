/*
 * Copyright 2017 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java'

group = 'ai.iterative.ec2agent.template'
version = '0.1'

// these values that go into plugin.xml
project.ext.pluginDesc = [
    id         : 'ai.iterative.elastic.agent.ec2',
    version    : project.version,
    goCdVersion: '19.3.0',
    name       : 'AWS EC2 Elastic Agent Plugin',
    description: '',
    vendorName : 'Iterative Inc',
    vendorUrl  : ''
]

// Force 1.7 compatibility to ensure that it works with older JVMs
sourceCompatibility = 1.8
targetCompatibility = 1.8

// In this section you declare where to find the dependencies of your project
repositories {
  mavenCentral()
  mavenLocal()
  jcenter()
}

sourceSets {
  test {
    java {
      compileClasspath += configurations.compileOnly
      runtimeClasspath += configurations.compileOnly
    }
  }
}

dependencies {
  implementation platform('com.amazonaws:aws-java-sdk-bom:1.11.687')
  implementation 'com.amazonaws:aws-java-sdk-ec2:1.11.687'

  compileOnly group: 'cd.go.plugin', name: 'go-plugin-api', version: '18.2.0'
  implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.5'
  implementation group: 'com.google.guava', name: 'guava', version: '23.0'
  implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.8.1'
  implementation group: 'joda-time', name: 'joda-time', version: '2.10.1'

  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.3.2'
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.4.1'
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.3.2'
  testImplementation group: 'org.hamcrest', name: 'hamcrest-library', version: '2.1'
  testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.25.1'
  testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '2.25.1'
  testImplementation group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.0'
}

processResources {
  from("src/main/resource-templates") {
    filesMatching('plugin.xml') {
      expand project.pluginDesc
    }
  }

  from("src/main/resources") {
    filesMatching('plugin.properties') {
      expand project.pluginDesc
    }
  }
}

test {
  useJUnitPlatform()
  testLogging {
    showStandardStreams = true

    exceptionFormat 'full'

    beforeSuite { suite ->
      if (suite.parent) {
        logger.quiet("Running ${suite.name}")
      }
    }

    afterSuite { suite, result ->
      if (suite.parent) {
        logger.quiet("Tests run: ${result.testCount}, Failures: ${result.failedTestCount}, Skipped: ${result.skippedTestCount}, Time elapsed: ${(result.endTime - result.startTime) / 1000.00} sec")
        if (result.resultType == TestResult.ResultType.FAILURE) {
          logger.quiet("Test ${suite.name} FAILED")
        }
      } else {
        logger.quiet("Total tests run: ${result.testCount}, Failures: ${result.failedTestCount}, Skipped: ${result.skippedTestCount}, Time elapsed: ${(result.endTime - result.startTime) / 1000.00} sec")
      }
    }
  }
}

jar {
  from(configurations.compile) {
    into "lib/"
  }
}
